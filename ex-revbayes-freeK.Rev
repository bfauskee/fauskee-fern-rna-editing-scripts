################################################################################
#
# RevBayes Example: Inferring ancestral states and rates of morphological evolution using the independent rates model.
#
# Note that this script is kept flexible so that you can easily substitute
# the character of interest.
#
# authors:  Sebastian HÃ¶hna
#
################################################################################

#######################
# Reading in the Data #
#######################

NUM_STATES = 2

# Import the morphological character matrix #
morpho <- readDiscreteCharacterData("filmy_start.nex")

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##############
# Tree model #
##############

# Here we use a fixed tree topology of primates
# Note that the readTrees function always returns a vector of trees,
# so we simple take the first tree
phylogeny <- readTrees("filmy-rbcl-bdp.tre")[1]


#########################
# Set up the rate model #
#########################

# we assume every rate i <-> j to be independent and exponentially distributed
rate_pr := phylogeny.treeLength() / 10
NUM_RATES = NUM_STATES * (NUM_STATES-1)
for ( i in 1:NUM_RATES ) {
    rate[i] ~ dnExp(rate_pr)
    moves.append( mvScale( rate[i], weight=2 ) )
}


##########################
# Set up the rate matrix #
##########################

Q_morpho := fnFreeK( rate, rescale=false )



#####################################
# Set up the root state frequencies #
#####################################

rf_prior <- rep(1,NUM_STATES)
rf ~ dnDirichlet( rf_prior )
moves.append( mvBetaSimplex( rf, weight=2 ) )
moves.append( mvDirichletSimplex( rf, weight=2 ) )



###################
# Set up the CTMC #
###################

# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, rootFrequencies=rf, type="Standard")
phyMorpho.clamp(morpho)



########
# MCMC #
########

# initialize the model object #
mymodel = model(phylogeny)
# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="filmy_start_treetraces/filmy_start_freeK.log", printgen=100) )
# 2. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=1000) )
# 3. add an ancestral state monitor
monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                  ctmc=phyMorpho,
                                                  filename="filmy_start_treetraces/filmy_start_freeK.states.txt",
                                                  type="Standard",
                                                  printgen=100,
                                                  withTips=true,
                                                  withStartStates=false) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=1000000, tuningInterval=200)

# check the performance of the MCMC/moves
mymcmc.operatorSummary()


# Read in the tree trace and construct the ancestral states (ASE) #
anc_states = readAncestralStateTrace("filmy_start_treetraces/filmy_start_freeK.states.txt")
num_sites = 5
# Loop over each site and generate the ancestral state tree
for (i in 1:num_sites) {
    # Create the file name for the output tree using concatenation
    file_name = "filmy_start_treetraces/start" + i + "_ase_freeK.tre"
    
    # Generate the ancestral state tree for each site
    anc_tree = ancestralStateTree(
        tree=phylogeny,
        ancestral_state_trace_vector=anc_states,
        include_start_states=false,
        file=file_name,
        burnin=0.25,
        summary_statistic="MAP",
        site=i
    )
}
# Quit RevBayes #
q()
